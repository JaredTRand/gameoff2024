import "res://world/dialogue/tipfish_eating.dialogue" as tipfish_eating

~ comehere
set GameState.dialogue_on = true
set GameState.met_fish = true
do GameState.swap_camera("fish")

Ferdinand: Pssssst.
Ferdinand: Hey.
Ferdinand: Come over here.

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END





~ introduction
set GameState.dialogue_on = true
set GameState.met_fish = true
set GameState.fish_introduction = true

do GameState.swap_camera("fish")
Ferdinand: You trying to get outside again?

do GameState.swap_camera("felix")
Felix: Yeah. I wanna go nap in the sun again!
Felix: It’s weird. It's hard to find a way out today.

do GameState.swap_camera("fish")
Ferdinand: Human isn’t happy since last time.
Ferdinand: They locked the joint up and hid away the keys.

do GameState.swap_camera("felix")
Felix: But I just wanted to go sleep in the sun!
Felix: This stinks.

do GameState.swap_camera("fish")
Ferdinand: …
Ferdinand: A little birdie might have told me of a way out.

do GameState.swap_camera("felix")
Felix: Really?!

do GameState.swap_camera("fish")
Ferdinand: Yes… I do recall something like that…
Ferdinand: My memory’s feeling a little fuzzy though.
Ferdinand: Some flakes might clear things up a little.

do GameState.swap_camera("felix")
Felix: Yeah, yeah, fine. I get it.
Felix: Where can I find some?

do GameState.swap_camera("fishflakes")
Ferdinand: Human left some up on a shelf up there. Go grab those for me.

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END




~ beforeFirstFlake
set GameState.dialogue_on = true

do GameState.swap_camera("fish")
Ferdinand: You got the stuff?

do GameState.swap_camera("felix")
Felix: I haven't found any yet.

do GameState.swap_camera("fish")
Ferdinand: ...
Ferdinand: So why are you here?
Ferdinand: My memories still feeling fuzzy.
Ferdinand: Some flakes will help. They're in a big orange container.
Ferdinand: Got a little fella like me on the front. You can't miss it.

do GameState.swap_camera("felix")
Felix: Right. On it.
set GameState.dialogue_on = false
=> END





~ firstFlakeandNoIntro
set GameState.dialogue_on = true
set GameState.met_fish = true
set GameState.fish_introduction = true

do GameState.swap_camera("fish")
Ferdinand: *sniff sniff*
Ferdinand: Are those flakes?
Ferdinand: They smell so fresh!
Ferdinand: Pour them on in and I'll tell you a secret.
~ firstFlake
set GameState.dialogue_on = true
set GameState.firstFlake = true
do GameState.use_fish_flake()
do GameState.swap_camera("fish")

=>< tipfish_eating/sounds
=>< tipfish_eating/comments
=>< tipfish_eating/sounds
=>< tipfish_eating/comments
Ferdinand: That's some quality flake.

do GameState.swap_camera("felix")
Felix: So...

do GameState.swap_camera("fish")
Ferdinand: Huh? Oh, right!
Ferdinand: Well, Carrie Cardinal was flying around the feeders today.
Ferdinand: She mentioned that a window in the top floor lookout was wide open. 

do GameState.swap_camera("felix")
Felix: Great! But how am I gonna get all the way up there?

do GameState.swap_camera("fish")
Ferdinand: What are you asking me for? I can’t do everything for you.
Ferdinand: Although… If you find some more flakes… my memory might clear up a little more… 
Ferdinand: Might just remember something helpful.
Ferdinand: My info’s good. Quality stuff.
Ferdinand: I’m no red herring, after all. 
Ferdinand: Those guys suck. Notorious liars.

do GameState.swap_camera("felix")
Felix: Right...
Felix: See ya!

set GameState.fish_intro_completed = true
set GameState.dialogue_on = false
=> END





~ fishtip_crowbar
set GameState.dialogue_on = true
do GameState.use_fish_flake()
do GameState.swap_camera("felix")
Felix: I need something that I can pry with.

do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
=>< tipfish_eating/comments
Ferdinand: Have you tried looking in some of the higher cabinets? Candy told me about some funny looking things up there.

do GameState.swap_camera("felix")
Felix: Who's Candy?

do GameState.swap_camera("fish")
Ferdinand: Candy. The cockroach who lives in human's pantry.

do GameState.swap_camera("felix")
Felix: Yuck.

do GameState.swap_camera("felix")
set GameState.dialogue_on = false
=> END

~ fishtip_key
do GameState.use_fish_flake()
set GameState.dialogue_on = true

do GameState.swap_camera("felix")
Felix: The door to upsairs is locked.

do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
=>< tipfish_eating/comments
Ferdinand: Try some of the cabinets in the kitchen? There must be lots of goodies in there.

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END

~ fishtip_grate
set GameState.dialogue_on = true
do GameState.use_fish_flake()
do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
=>< tipfish_eating/sounds
=>< tipfish_eating/comments

do GameState.swap_camera("felix")
Felix: Any tips for me?

do GameState.swap_camera("fish")
Ferdinand: Don't forget to look up once in a while. Elenor has told me a thing or two about tunnels or some such. 

do GameState.swap_camera("felix")
Felix: Tunnels? Wait, who is Elenor?

do GameState.swap_camera("fish")
Ferdinand: Wouldn't you like to know.

do GameState.swap_camera("felix")
Felix: Yeah, kinda.

do GameState.swap_camera("fish")
Ferdinand: Go on, get out of here. Keep looking.

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END


~ fishtip_screwdriver
set GameState.dialogue_on = true
do GameState.use_fish_flake()
do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
=>< tipfish_eating/sounds
=>< tipfish_eating/comments

do GameState.swap_camera("felix")
Felix: Got any more tips for me?

do GameState.swap_camera("fish")
Ferdinand: Sure do.
Ferdinand: Word around town is that Human is getting their bathroom worked on.

do GameState.swap_camera("felix")
Felix: ...
Felix: So?

do GameState.swap_camera("fish")
Ferdinand: Look, I can't spell everything out for you, kid.
Ferdinand: Maybe the info is useful, maybe it's not. Not my problem.
Ferdinand: They're not all zingers.

do GameState.swap_camera("felix")
Felix: Right. Okay then.

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END


~ fishtip_scissors
set GameState.dialogue_on = true
do GameState.use_fish_flake()
do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
=>< tipfish_eating/comments
=>< tipfish_eating/sounds
=>< tipfish_eating/comments

do GameState.swap_camera("felix")
Felix: Got any more tips for me?

do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
Ferdinand: Sure.
Ferdinand: Check in the tiny human's room. Tiny humans are well know for being kleptomaniacs.

do GameState.swap_camera("felix")
Felix: Really?

do GameState.swap_camera("fish")
Ferdinand: Nope.
Ferdinand: Well, sort of.
Ferdinand: Ah, whatever, It doesn't matter. Just try looking up there!

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END


~ flakeless
set GameState.dialogue_on = true
do GameState.swap_camera("felix")
Felix: Hey Ferdi! Got any good tips for me?

do GameState.swap_camera("fish")
Ferdinand: That depends. Do you have any good flakes for me?

do GameState.swap_camera("felix")
Felix: ...
Felix: No...

do GameState.swap_camera("fish")
Ferdinand: Then get back out there!

do GameState.swap_camera("felix")
Felix: I can't come just to say hi?

do GameState.swap_camera("fish")
Ferdinand: Not when we have a mission to complete!

do GameState.swap_camera("felix")
set GameState.dialogue_on = false
=> END


~ noMoreTips
set GameState.dialogue_on = true
do GameState.use_fish_flake()

do GameState.swap_camera("fish")
=>< tipfish_eating/sounds
=>< tipfish_eating/comments
Ferdinand: Thank you for the flakes, Felix, but I'm fresh out of tips. 

do GameState.swap_camera("felix")
Felix: Really?

do GameState.swap_camera("fish")
Ferdinand: Yes. I have told you everything that I know.
Ferdinand: The student becomes the master.
Ferdinand: Now go. Get out there, and live!
Ferdinand: Though do come back to give me flakes sometimes.

do GameState.swap_camera("felix")
Felix: Sure thing. Bye!

do GameState.swap_camera("fish")
Ferdinand: *Sniffle* Goodbye, young Felix...

do GameState.swap_camera("felix")
Felix: Ferdi, I'm not leaving forever. I just need a good sun nap.

do GameState.swap_camera("fish")
Ferdinand: Aaaaand... scene! How did you like my acting? Was I convincing?
Ferdinand: I taught myself how to fake cry and everything.
Ferdinand: ...
Ferdinand: It gets lonely in here.

do GameState.swap_camera("felix")
Felix: I'll come visit again soon then.

do GameState.swap_camera("fish")
Ferdinand: I'd like that.
Ferdinand: Bye for now.

do GameState.swap_camera("felix")
Felix: Bye Ferdi!

do GameState.swap_camera("felix", false)
set GameState.dialogue_on = false
=> END